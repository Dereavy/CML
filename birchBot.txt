UNSAFE(0)
LOG("&a[birchBot]&7 Start")

// Setting to talk out-loud about status changes (chests full, need materials, etc.)
SET(@chat_logs)
// Common elements that hook into command-status system
@&status = "Running"
@&identity = "Birch Tree Bot"
UNSET(@paused)

#yaw_north = 360
#yaw_east = 450
#yaw_south = 180
#yaw_west = 270

#default_pitch = 50

// Ensure first check will always say block changed
#pos_x = %XPOS% - 1
#pos_z = %ZPOS% - 1

DO
    IF(!@paused)
        // Look to default position
        LOOKS(+0,%#default_pitch%,.2)
        
        // Has our block position changed?
        IF((%#pos_x%!=%XPOS%)||(%#pos_z%!=%ZPOS%))
        
            // Reset movement timer
            #movement_timer = 0
        
            // Get block standing on
            GETIDREL(0,-1,0,&standing_block)
        
            // Are we standing on cobblestone stairs?
            IF(%&standing_block%="stone_stairs")
        
                KEYUP(forward)
            
                // The direction of the chests is encoded by an adjacent crafting  table.  We must
                // check each of the cardinal directions.
            
                GETIDREL(0,-1,-1,&north_block)
                GETIDREL(0,-1,1,&south_block)
                GETIDREL(1,-1,0,&east_block)
                GETIDREL(-1,-1,0,&west_block)
            
                IF(%&north_block% = "crafting_table")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%&east_block% = "crafting_table")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%&south_block% = "crafting_table")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%&west_block% = "crafting_table")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF
            
                WAIT(5t)
            
                // Now that we are looking at the chests, we invoke the correctness verifier
            
                UNSET(@birchbot_handle_chests)
                EXEC("birchBot_handleChests.txt")
                DO
                    WAIT(1t)
                UNTIL(@birchbot_handle_chests)

                // Build 4 axes, if we need them

                UNSET(@birchbot_handle_axes)
                EXEC("birchBot_handleAxes.txt")
                DO
                    WAIT(1t)
                UNTIL(@birchbot_handle_axes)
            
                // Now we visually inspect the tile to decode which direction to initially walk
            
                LOOKS(+0,90,.2)
                WAIT(5t)
            
                IF(%HIT_FACING% = "north")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "east")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "south")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "west")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF
            
                KEYDOWN(forward)
        
            // Are we standing on grass or dirt?
            ELSEIF((%&standing_block%="grass")||(%&standing_block%="dirt"))

                // Destroy top of tree, if any exists

                UNSET(@treebot_treetop)
                EXEC("birchBot_treeTop.txt")
                DO
                    WAIT(1t)
                UNTIL(@treebot_treetop)

                // We look in the four cardinal directions for a stone_slab
                IF(%DIRECTION% = "N")
                    GETIDREL(0,-1,1,&block_behind)
                ELSEIF(%DIRECTION% = "E")
                    GETIDREL(-1,-1,0,&block_behind)
                ELSEIF(%DIRECTION% = "S")
                    GETIDREL(0,-1,-1,&block_behind)
                ELSEIF(%DIRECTION% = "W")
                    GETIDREL(1,-1,0,&block_behind)
                ENDIF

                IF(%&block_behind% = "birch_stairs")

                    IF(%DIRECTION% = "N")
                        GETIDREL(-1,-1,0,&block_left)
                        GETIDREL(1,-1,0,&block_right)

                    ELSEIF(%DIRECTION% = "E")
                        GETIDREL(0,-1,-1,&block_left)
                        GETIDREL(0,-1,1,&block_right)

                    ELSEIF(%DIRECTION% = "S")
                        GETIDREL(1,-1,0,&block_left)
                        GETIDREL(-1,-1,0,&block_right)

                    ELSEIF(%DIRECTION% = "W")
                        GETIDREL(0,-1,1,&block_left)
                        GETIDREL(0,-1,-1,&block_right)

                    ENDIF

                    IF(%&block_left% = "stone_slab")
                        KEYUP(forward)
                        LOOKS(-90,%#default_pitch%,.2)
                        WAIT(3t)
                        SET(ignore_slab)

                    ELSEIF(%&block_right% = "stone_slab")
                        KEYUP(forward)
                        LOOKS(+90,%#default_pitch%,.2)
                        WAIT(3t)
                        SET(ignore_slab)
                    ENDIF

                    KEYDOWN(forward)

                ENDIF

            // Are we standing on stone brick stairs?
            ELSEIF(%&standing_block%="stone_brick_stairs")

                KEYUP(forward)

                LOOKS(+0,90,.2)
                WAIT(5t)
            
                IF(%HIT_FACING% = "north")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "east")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "south")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "west")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF

                WAIT(4t)

                KEYDOWN(forward)
        
            // Are we standing on birch stairs?
            ELSEIF(%&standing_block%="birch_stairs")
        
                // Look towards birch stairs direction

                KEYUP(forward)
                
                LOOKS(+0,90,.2)
                WAIT(5t)
            
                IF(%HIT_FACING% = "north")
                    LOOKS(%#yaw_north%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "east")
                    LOOKS(%#yaw_east%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "south")
                    LOOKS(%#yaw_south%,%#default_pitch%,.2)
            
                ELSEIF(%HIT_FACING% = "west")
                    LOOKS(%#yaw_west%,%#default_pitch%,.2)
                ENDIF

                // Destroy front of tree, if any exists, and plant saplings
        
                UNSET(@treebot_treefront)
                EXEC("birchBot_treeFront.txt")
                DO
                    WAIT(1t)
                UNTIL(@treebot_treefront)
            ENDIF

            // Only save position when we actually have executed a new block
            #pos_x = %XPOS%
            #pos_z = %ZPOS%

        ENDIF

        // Walk
        KEYDOWN(forward)
        
        // Increment movement timer
        INC(#movement_timer)
        
        // Has our position changed in the past 60 seconds? (excluding external scripts)
        IF(%#movement_timer%>1200)
            // Rotate 90 degrees clockwise
            LOOKS(+90,%#default_pitch%,.2)
        ENDIF

        $$<"eatFood.txt">

        // Snap yaw
        IF(%DIRECTION% = "N")
            LOOK(%#yaw_north%,+0)
        ELSEIF(%DIRECTION% = "E")
            LOOK(%#yaw_east%,+0)
        ELSEIF(%DIRECTION% = "S")
            LOOK(%#yaw_south%,+0)
        ELSEIF(%DIRECTION% = "W")
            LOOK(%#yaw_west%,+0)
        ENDIF
    ELSE
        // If paused, we rotate so it's obvious
        KEYUP(forward)

        #pause_rotate_yaw = %#pause_rotate_yaw% + 2

        IF(%#pause_rotate_yaw% >= 540)
            #pause_rotate_yaw = 180
        ELSEIF(%#pause_rotate_yaw% < 180)
            #pause_rotate_yaw = 180
        ENDIF

        LOOK(%#pause_rotate_yaw%, 50)
    ENDIF

    WAIT(1t)

WHILE(1)

ENDUNSAFE()
    


