#yaw_north = 360
#yaw_east = 450
#yaw_south = 180
#yaw_west = 270

#default_pitch = 80

UNSAFE(0)

// Ensure first check will always say block changed
#pos_x = %XPOS%-1
#pos_z = %ZPOS%-1

DO

    //#delta_ticks=%DAYTICKS%-%#day_ticks%
    //LOG(%#delta_ticks%)
    //#day_ticks=%DAYTICKS%

    // Walk
    KEYDOWN(forward)
    KEYDOWN(sprint)
    
    // Look to default position
    LOOKS(+0,%#default_pitch%,.2)
    
    // Has our block position changed?
    IF((%#pos_x%!=%XPOS%)||(%#pos_z%!=%ZPOS%))

        LOG(%ZPOS%)

        // Reset movement timer
        #movement_timer = 0
    
        // Get block standing on
        GETIDREL(0,-1,0,&standing_block)
        GETIDREL(0,0,0,&occupied_block)

        // Are we standing on a cobblestone stair?
        IF((%&standing_block%="stone_stairs")||(%&occupied_block%="stone_stairs"))
    
            KEYUP(forward)
        
            // The direction of the chest matrix is encoded by an adjacent stone slab.  We must
            // check each of the cardinal directions.
        
            GETIDREL(0,-1,-1,&north_block)
            GETIDREL(0,-1,1,&south_block)
            GETIDREL(1,-1,0,&east_block)
            GETIDREL(-1,-1,0,&west_block)
        
            IF(%&north_block% = "stone_slab")
                LOOKS(%#yaw_north%,%#default_pitch%,.2)
        
            ELSEIF(%&east_block% = "stone_slab")
                LOOKS(%#yaw_east%,%#default_pitch%,.2)
        
            ELSEIF(%&south_block% = "stone_slab")
                LOOKS(%#yaw_south%,%#default_pitch%,.2)
        
            ELSEIF(%&west_block% = "stone_slab")
                LOOKS(%#yaw_west%,%#default_pitch%,.2)
            ENDIF
        
            WAIT(5t)
        
            // Now that we are looking at the chest set, we place our items appropriately
        
            UNSET(@wheat_handle_chests)
            EXEC("wheatBot_handleChests.txt")
            DO
                WAIT(1t)
            UNTIL(@wheat_handle_chests)
        
            // Now we visually inspect the tile to decode which direction to initially walk
        
            LOOKS(+0,90,.2)
            WAIT(5t)
        
            IF(%HIT_FACING% = "north")
                LOOKS(%#yaw_north%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "east")
                LOOKS(%#yaw_east%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "south")
                LOOKS(%#yaw_south%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "west")
                LOOKS(%#yaw_west%,%#default_pitch%,.2)
            ENDIF
        
            KEYDOWN(forward)

        // Are we standing on birch stairs?
        ELSEIF((%&standing_block%="birch_stairs")||(%&occupied_block%="birch_stairs"))

            KEYUP(forward)

            LOOKS(+0,90,.2)
            WAIT(5t)
        
            IF(%HIT_FACING% = "north")
                LOOKS(%#yaw_north%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "east")
                LOOKS(%#yaw_east%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "south")
                LOOKS(%#yaw_south%,%#default_pitch%,.2)
        
            ELSEIF(%HIT_FACING% = "west")
                LOOKS(%#yaw_west%,%#default_pitch%,.2)
            ENDIF

            WAIT(4t)

            KEYDOWN(forward)

        ELSEIF((%&standing_block%="farmland")||(%&occupied_block%="farmland"))

            // Look down (if we aren't already)
            LOOK(+0,80)
            PICK("wheat_seeds")

            IF(%HITID%="wheat")
                // If the plant is wheat, and it's fully grown, harvest it
                IF(%HIT_AGE%=7)
                    KEY(attack)
                    WAIT(1t)
                    KEY(use)
                ENDIF

            ELSEIF(%HITID%="farmland")
                // If the farmland is empty, plant a seed
                KEY(use)
            ENDIF            

        ENDIF

        // Only save position when we actually have executed a new block
        #pos_x = %XPOS%
        #pos_z = %ZPOS%

    ENDIF
    
    // Increment movement timer
    INC(#movement_timer)
    
    // Has our position changed in the past 60 seconds? (excluding external scripts)
    IF(%#movement_timer%>1200)
        // Rotate 90 degrees clockwise
        LOOKS(+90,%#default_pitch%,.2)
    ENDIF

    $$<"eatFood.txt">

    // Snap yaw
    IF(%DIRECTION% = "N")
        LOOK(%#yaw_north%,+0)
    ELSEIF(%DIRECTION% = "E")
        LOOK(%#yaw_east%,+0)
    ELSEIF(%DIRECTION% = "S")
        LOOK(%#yaw_south%,+0)
    ELSEIF(%DIRECTION% = "W")
        LOOK(%#yaw_west%,+0)
    ENDIF

    WAIT(1t)

WHILE(1)

ENDUNSAFE()
    


