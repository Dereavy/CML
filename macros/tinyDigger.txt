#z_max = 8336
#z_min = 8327
#scatter = 8

#i = 0
UNSAFE(0)
DO
	WAIT(1t)

    &iter_pick = "stone redstone_ore lit_redstone_ore lapis_ore gold_ore coal_ore iron_ore cobblestone"
    &iter_pick[] = SPLIT(" ",%&iter_pick%)

    &iter_shovel = "dirt sand gravel"
    &iter_shovel[] = SPLIT(" ",%&iter_shovel%)

    // Are we currently looking at a 'target' area?

    IF(((%HITY% == %YPOS%) || (%HITY% == %YPOS% + 1)) && (%HITZ% >= %#z_min%) && (%HITZ% <= %#z_max%))

    	GETID(%HITX%,%HITY%,%HITZ%,&id)

    	is_pick_block = false
    	FOREACH(&iter_pick,&block)
    		IF(%&id% == %&block%)
    			is_pick_block = true
    			BREAK
    		ENDIF
    	NEXT

    	is_shovel_block = false
    	FOREACH(&iter_shovel,&block)
    		IF(%&id% == %&block%)
    			is_shovel_block = true
    			BREAK
    		ENDIF
    	NEXT

    	IF((%is_pick_block%) && (%ITEM% != "diamond_pickaxe"))
    		PICK("diamond_pickaxe")
    		WAIT(1t)
    	ENDIF

    	IF((%is_shovel_block%) && (%ITEM% != "diamond_shovel"))
    		PICK("diamond_shovel")
    		WAIT(1t)
    	ENDIF

    	IF(is_pick_block || is_shovel_block)
    		KEY(attack)
    	ENDIF
    ENDIF

    // If we currently have a tool selected and its low durability, stop completely for safety
    IF(((%ITEM% == "diamond_pickaxe") || (%ITEM% == "diamond_shovel")) && (%DURABILITY% <= 50))
    	IF(%SLOT% == 1)
    		SLOT(2)
    		STOP
    	ELSE
    		SLOT(1)
    		STOP
    	ENDIF
    ENDIF

    SPRINT

    IF(%#i% == 0)
    	LOOK(+%#scatter%,+0)
    ELSEIF(%#i% == 4)
    	LOOK(-%#scatter%,+0)
  	ENDIF

    INC(#i)
    IF(%#i% >= 8)
    	#i = 0
    ENDIF
LOOP
ENDUNSAFE
